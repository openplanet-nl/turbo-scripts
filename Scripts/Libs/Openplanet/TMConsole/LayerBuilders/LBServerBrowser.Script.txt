#Include "Libs/Openplanet/Manialink2.Script.txt"								as Manialink
#Include "Libs/Openplanet/TMConsole/Menu/LibLoading.Script.txt"				as Loading
#Include "Libs/Openplanet/TMConsole/PanelParameter.Script.txt"	            as PanelParameter
#Include "Libs/Openplanet/TMConsole/Live/LiveHelpers.Script.txt"				as LiveHelpers
#Include "Libs/Openplanet/TMConsole/Menu/LibOnlineGameplay.Script.txt"		as GamePlay
#Include "Libs/Openplanet/UIPadButtons.Script.txt"							as LibUIButtons

#Include "Libs/Openplanet/TMConsole/LayerBuilders/LBServerBrowser_Filters.Script.txt" 	as Filters
#Include "Libs/Openplanet/TMConsole/LayerBuilders/LBServerBrowser_ServerInfo.Script.txt" as ServerInfo
#Include "Libs/Openplanet/TMConsole/LayerBuilders/LBServerBrowser_Debug.Script.txt" 		as Debug
#Include "Libs/Openplanet/TMConsole/LayerBuilders/LBServerBrowser_Browser.Script.txt" 	as Browser

#Const C_ImgBase 	"file://Media/Manialinks/Nadeo/TMConsole/Images/"

#Const Count_InstancesY				11

Text Build(Text _ShowAnim, Text _HideAnim) {

	//IMAGES
	declare Text IMGBoard       	= C_ImgBase^"board_Empty.tga";	
	declare Text IMGEchelonPrefix  	= C_ImgBase^"Online/BrowseServers/echelons/Echelon_";
	
	//SIZES
	declare Real	SM_SizeTotalX            = 259.0;
	declare Real	SM_SizeTotalY            = 150.;
	declare Real	SM_SizeY                 = 8.27;

	//POSITIONS
	declare Real	PositionFirstQuadX       = 0.;
	declare Real	PositionFirstQuadY       = 36.3;

	declare Integer Platform = LibUIButtons::GetPlatform();
	
	Loading::Init(<SM_SizeTotalX, SM_SizeY*Count_InstancesY>, <PositionFirstQuadX, (PositionFirstQuadY - (SM_SizeY*(Count_InstancesY+1)) / 2.)>, 400, _("Retrieving rooms. Please wait."));
	Filters::Init();
	ServerInfo::Init(True, False);

	declare MLText = """	
		<frame id="Frame_General">
			<quad id="Quad_Board" posn="0 0 0" halign="center" valign="center" sizen="{{{259.5}}} {{{SM_SizeTotalY}}}" opacity="1.0" image="{{{IMGBoard}}}" />
	""";

	/////////////////////////
	// LOADING
	/////////////////////////		
	MLText ^= """{{{Loading::InjectInManialink()}}}""";
	
	/////////////////////////
	// BROWSER
	/////////////////////////
	MLText ^= """{{{Browser::InjectInManialink()}}}""";
	
	/////////////////////////
	// SERVER INFO
	/////////////////////////
	MLText ^= """{{{ServerInfo::InjectInManialink()}}}""";
	
	/////////////////////////
	// FILTERS
	/////////////////////////
	MLText ^= """{{{Filters::InjectInManialink()}}}""";
	
	/////////////////////////
	// DEBUG
	/////////////////////////
	MLText ^= """{{{Debug::InjectInManialink()}}}""";
	
	MLText ^= """
	</frame>
	
	<script><!--
		#Const DebugName  "L_ServerBrowser"
		#Const C_IsServerOnlineRequestDelay		2000 //on attend 2s entre chaque requete
		#Const C_TimeTransitionPage2  {{{PanelParameter::GetTimeTransitionPage2()}}}
		#Const C_ServerInfoLists_Speed 100
		
		#Const C_STATUS_NONE 		0
		#Const C_STATUS_BROWSER		1
		#Const C_STATUS_FILTERS		2
		#Const C_STATUS_INFORMATION	3
		#Const C_STATUS_JOIN		4
		#Const C_STATUS_DEBUG		5
		
		{{{ServerInfo::InjectInConst()}}}
		
		#Include "TextLib" as TL
		
		{{{Manialink::GetIncludes()}}}
		{{{Manialink::Load()}}}
		
		declare Integer						G_LockInput;
		declare Integer						G_HideTime;
		declare Integer 					G_ServerBrowserStatus;
		declare Boolean						G_IsLimitedToCanyon;
		
		{{{Browser::InjectInGlobals()}}}
		{{{Filters::InjectInGlobals()}}}
		{{{ServerInfo::InjectInGlobals()}}}
		{{{Debug::InjectInGlobals()}}}
		
		declare Boolean						G_SelectLastRoom;
		declare Boolean						G_IsInRoomManager;
		
		declare Boolean						G_RequestJoinServer;
		
		declare Integer						G_IsServerOnlineRequestLastTime;
		declare Boolean						G_IsWaitingForServerStarted;	//wait the "running" state for the server 
		declare Text						G_IsWaitingForServer_ServerId;	//the server we're waiting
	
		//Gameplay
		declare Text[]						G_RoomsStyles;
		declare Text[]						G_RoomsDurations;
		declare Text[]						G_RoomsGameModesDisplayNames;
		
		//Images
		declare Text[] G_Images_Echelon;
		
		{{{Loading::InjectInGlobals()}}}
		{{{Loading::InjectInFunctions()}}}
		
		{{{LiveHelpers::InjectConverters()}}}
		{{{LiveHelpers::InjectFilters()}}}
		
		Void HidePage()
		{
			G_HideTime  = Now + C_TimeTransitionPage2;
			G_LockInput = Now + C_TimeTransitionPage2;
		}
			
		{{{LibUIButtons::Inject_SetInputInfoTextSelected()}}}
		
		Void ToggleInputInfo(Text _Name, Boolean _Hovered) {
			declare CMlFrame FrameInput <=> (Page.GetFirstChild(_Name) as CMlFrame);
			declare CMlLabel LabelInputInfo <=> (FrameInput.GetFirstChild("Label_Name") as CMlLabel);
			SetInputInfoTextSelected(LabelInputInfo, _Hovered);
		}
			
		{{{Browser::InjectInFunctions()}}}
		{{{Filters::InjectInFunctions()}}}
		{{{ServerInfo::InjectInFunctions()}}}
		{{{Debug::InjectInFunctions()}}}
		
		//////////////////
		// STATUS SWITCH
		//////////////////
		Void ServerBrowserStatus_Switch(Integer _NewStatus)
		{
			switch (G_ServerBrowserStatus)
			{
				//case C_STATUS_BROWSER 		: Browser_Hide(); //A priori le browser ne fait rien quand il se cache
				case C_STATUS_FILTERS 		: Filters_Hide();
				case C_STATUS_INFORMATION 	: ServerInfo_Hide();
				//case C_STATUS_JOIN 			: Join_Hide(); //JOIN S'OCCUPE DE LUI COMME UN GRAND
				case C_STATUS_DEBUG 		: Debug_Hide();
			}
			
			G_ServerBrowserStatus = _NewStatus;
			
			switch (G_ServerBrowserStatus)
			{
				case C_STATUS_BROWSER 		: Browser_Show();
				case C_STATUS_FILTERS 		: Filters_Show();
				case C_STATUS_INFORMATION 	: G_MustDisplayServerInfo = True;
				case C_STATUS_JOIN 			: G_RequestJoinServer = True;
				case C_STATUS_DEBUG 		: Debug_Show();
			}
		}
		
		Void JoinServer()
		{	
			if (TitleFlow.JoinServer_ServerInfo == Null)
				return;
				
			//Check LP
			if (LocalUser.LadderPoints < TitleFlow.JoinServer_ServerInfo.LadderServerLimitMin)
			{
				SendCustomEvent("ServerBrowser_PopUp_NotEnoughLP", [TL::ToText(LocalUser.LadderPoints), TL::ToText(TitleFlow.JoinServer_ServerInfo.LadderServerLimitMin)]);
				SetJoinStatus(C_JOINSTATUS_NONE);
				return;
			}
			//Check isFull
			if (TitleFlow.JoinServer_ServerInfo.PlayerCount >= TitleFlow.JoinServer_ServerInfo.MaxPlayerCount)
			{
				SendCustomEvent("ServerBrowser_PopUp_RoomIsFull", Text[]);
				SetJoinStatus(C_JOINSTATUS_NONE);
				ServerBrowserStatus_Switch(C_STATUS_BROWSER);
				return;
			}
			
			PresenceMgr.SetPresence(DataMgr.MenuUserId, CPresenceMgr::ERichPresence::Multi);
			
			TitleFlow.JoinServer_Join(False);
			declare Integer LastUpdate = Now;
			while(!TitleFlow.IsReady)
			{
				if (Now - LastUpdate >= 30000)
					break;
				LastUpdate = Now;
				LibManialink_AnimLoop();
				ProgressBar_Update();
				LibManialink_AnimLoop();
				yield;
			}
			
			SetJoinStatus(C_JOINSTATUS_NONE);
			ServerBrowserStatus_Switch(C_STATUS_BROWSER);
			PresenceMgr.SetPresence(DataMgr.MenuUserId, CPresenceMgr::ERichPresence::MainMenu);
			SendCustomEvent("OnExitRoom", Text[]);
		}
		
		Void UpdateJoin()
		{
			declare Text[Text][] RequestedRooms for Page;
			declare Text[Text] FocusedRoom = RequestedRooms[G_CurrentSelectedRoomIndex];
			if (G_Join_Started && RequestedRooms[G_CurrentSelectedRoomIndex]["status"] == GetStatus("Error") || ProgressBar_IsTimeout())
			{
				SetJoinStatus(C_JOINSTATUS_ERROR);
				return;
			} else if (FocusedRoom["status"] == GetStatus("Running") && FocusedRoom["required_state"] == GetRequiredState(True)) {	
				G_IsServerOnlineRequestLastTime = Now;
				declare ServerName = FocusedRoom["dedicated_login"];
				wait(TitleFlow.IsReady);
				if (FocusedRoom["is_private"] == "true")
				{
					TitleFlow.JoinServer_GetInfo(ServerName, G_ServerInfo_JoinPassword);
				} else {
					TitleFlow.JoinServer_GetInfo(ServerName);
				}
				wait(TitleFlow.IsReady);
				if (TitleFlow.LatestResult == CTitleFlow::EResult::Error_Net_ServerUnreachable)
				{
					SetJoinStatus(C_JOINSTATUS_ERROR);
					return;
				}
				if (TitleFlow.JoinServer_ServerInfo != Null)
				{
					SetJoinStatus(C_JOINSTATUS_JOIN);
					JoinServer();
				} else {
					SetJoinStatus(C_JOINSTATUS_WAITMS);	
				}
			} else {
				//Live poll
				SendCustomEvent("RequestServerStatus", [FocusedRoom["id"]]);
				G_IsServerOnlineRequestLastTime = -1;
				SetJoinStatus(C_JOINSTATUS_STARTING);
			}
		}
		
		Void UpdateStatusForJoin()
		{
			G_RequestJoinServer = False;
			declare Text[Text][] RequestedRooms for Page;
			declare Text[Text] FocusedRoom = RequestedRooms[G_CurrentSelectedRoomIndex];
			if (LocalUser.LadderPoints < TL::ToInteger(FocusedRoom["ladder_limit_min"]))
			{
				SendCustomEvent("ServerBrowser_PopUp_NotEnoughLP", [TL::ToText(LocalUser.LadderPoints), FocusedRoom["ladder_limit_min"]]);
				SetJoinStatus(C_JOINSTATUS_NONE);
				return;
			}
			SendCustomEvent("RequestServerStatus", [FocusedRoom["id"]]);
			SetJoinStatus(C_JOINSTATUS_STATUS);
		}
		
		Void StartJoin()
		{
			declare Text[Text][] RequestedRooms for Page;
			declare Text[Text] FocusedRoom = RequestedRooms[G_CurrentSelectedRoomIndex];
			
			//on essaye de start même si on a un statut d'erreur
			if (FocusedRoom["status"] == GetStatus("Running"))
			{
				wait(TitleFlow.IsReady);
				if (FocusedRoom["is_private"] == "true")
				{
					TitleFlow.JoinServer_GetInfo(FocusedRoom["dedicated_login"], G_ServerInfo_JoinPassword);
				} else {
					TitleFlow.JoinServer_GetInfo(FocusedRoom["dedicated_login"]);
				}
				wait(TitleFlow.IsReady);		
				ProgressBar_Reset();
				ProgressBar_Show();
				if (TitleFlow.LatestResult == CTitleFlow::EResult::Error_Net_ServerUnreachable)
				{
					SetJoinStatus(C_JOINSTATUS_ERROR);
					return;
				}
				if (TitleFlow.JoinServer_ServerInfo != Null)
				{
					ProgressBar_StartAt("Join");
					SetJoinStatus(C_JOINSTATUS_JOIN);
					JoinServer();
				} else {
					ProgressBar_StartAt("MS");
					G_IsServerOnlineRequestLastTime = 0;
					SetJoinStatus(C_JOINSTATUS_WAITMS);
				}
			} else {
				ProgressBar_Reset();
				ProgressBar_Show();
				ProgressBar_StartAt("Start");
				G_IsServerOnlineRequestLastTime = 0;
				if (FocusedRoom["required_state"] == GetRequiredState(False))
				{
					G_Join_Started = False;
					if (FocusedRoom["is_private"] == "true")
						SendCustomEvent("RequestServerStart", [FocusedRoom["id"], G_ServerInfo_JoinPassword]);
					else
						SendCustomEvent("RequestServerStart", [FocusedRoom["id"], ""]);
				}
				SetJoinStatus(C_JOINSTATUS_STARTING);
			}
		}
		
		Void Open()
		{
			ServerBrowserStatus_Switch(C_STATUS_BROWSER);
		}
		
		Void Quit()
		{
			G_IsWaitingForServerStarted = False;
			G_IsWaitingForServer_ServerId = "";
			G_IsServerOnlineRequestLastTime = 0;
			Filters_Reset();
			{{{_HideAnim}}}
			SendCustomEvent("GotoPrevious", [""]);
		}
		
		Void EditSelectedRoom()
		{
			G_IsInRoomManager = True;
			declare Text[Text][] RequestedRooms for Page;
			declare Text RoomIndex = RequestedRooms[G_IndexFocusY]["id"];
			SendCustomEvent("SetEditedRoomIndex", [RoomIndex]);
			SendCustomEvent("GotoPage", ["RoomManager"]);
			{{{_HideAnim}}}
			SendCustomEvent("DetachPage", ["ServerBrowser"]);
		}
		
		Void CreateNewRoom()
		{
			G_IsInRoomManager = True;
			SendCustomEvent("SetEditedRoomIndex", ["-1"]);	
			SendCustomEvent("GotoPage", ["RoomManager"]);
			{{{_HideAnim}}}
			SendCustomEvent("DetachPage", ["ServerBrowser"]);
		}
		
		Void ShowServerStart()
		{
			//HideServerInfo();
			declare CMlFrame Frame_ServerJoin <=> (Page.GetFirstChild("Frame_ServerJoin") as CMlFrame);
			Frame_ServerJoin.Show();
			
			declare CMlFrame FrameInputCancel <=> (Frame_ServerJoin.GetFirstChild("FrameInputCancel") as CMlFrame);
			FrameInputCancel.Show();
			
			declare Text[Text][] RequestedRooms for Page;
			declare Text[Text] FocusedRoom = RequestedRooms[G_PageIndex * {{{Count_InstancesY}}} + G_IndexFocusY];
			G_IsWaitingForServer_ServerId = FocusedRoom["id"];
			G_IsWaitingForServerStarted = True;
			
			declare CMlLabel ServerJoinStatus <=> (Frame_ServerJoin.GetFirstChild("ServerJoinStatus") as CMlLabel);
			
			if (FocusedRoom["required_state"] == "1")
				ServerJoinStatus.Value = _("Requesting server start");
			else
				ServerJoinStatus.Value = _("Server is starting");
		}
		
		Void ServerStart_OnStarting()
		{
			declare CMlFrame Frame_ServerJoin <=> (Page.GetFirstChild("Frame_ServerJoin") as CMlFrame);
			declare CMlLabel ServerJoinStatus <=> (Frame_ServerJoin.GetFirstChild("ServerJoinStatus") as CMlLabel);
			ServerJoinStatus.Value = _("Server is starting");
		}
		
		Void HideServerStart()
		{
			G_MustDisplayServerInfo = True;
			declare CMlFrame Frame_ServerJoin <=> (Page.GetFirstChild("Frame_ServerJoin") as CMlFrame);
			Frame_ServerJoin.Hide();
			
			G_IsWaitingForServerStarted = False;
			G_IsServerOnlineRequestLastTime = 0;
			G_IsWaitingForServer_ServerId = "";
		}
		
		//////////////////
		// INIT
		//////////////////
		Void Init()
		{
			G_IsLimitedToCanyon = False;
		
			G_SelectLastRoom = False;
			G_ServerBrowserStatus = C_STATUS_NONE;
			G_IsInRoomManager = False;
			
			{{{Loading::InjectInInit()}}}
			
			{{{Browser::InjectInInit()}}}
			{{{ServerInfo::InjectInInit()}}}
			{{{Filters::InjectInInit()}}}
			{{{Debug::InjectInInit()}}}
			
			G_RoomsStyles = {{{dump(GamePlay::GetRoomStyles())}}};
			G_RoomsDurations = {{{dump(GamePlay::GetDurations())}}};
			G_RoomsGameModesDisplayNames = {{{dump(GamePlay::GetGameModesDisplayNames())}}};
			
			G_IsWaitingForServerStarted = False;
			G_IsWaitingForServer_ServerId = "";
			G_IsServerOnlineRequestLastTime = 0;
			
			G_RequestJoinServer = False;
			
			G_Images_Echelon = ["{{{IMGEchelonPrefix}}}0.tga", 
								"{{{IMGEchelonPrefix}}}1.tga", 
								"{{{IMGEchelonPrefix}}}2.tga",
								"{{{IMGEchelonPrefix}}}3.tga", 
								"{{{IMGEchelonPrefix}}}4.tga", 
								"{{{IMGEchelonPrefix}}}5.tga", 
								"{{{IMGEchelonPrefix}}}6.tga", 
								"{{{IMGEchelonPrefix}}}7.tga",	
								"{{{IMGEchelonPrefix}}}8.tga", 
								"{{{IMGEchelonPrefix}}}9.tga"];	
		}
		
		Void OnRequestedRoomsReceived(Integer _Onglet)
		{
			declare Text[Text][] RequestedRooms for Page;
			
			G_RequestError = False;
			if (G_OngletFocus != _Onglet)
				return;
			Loading_SetIsLoading(False);
			
			if (_Onglet < 2)
			{
				declare Real NbPages = (RequestedRooms.count*1.0 - 1)/({{{Count_InstancesY}}}*1.0);
				G_Prev_PageIndex = G_PageIndex;
				SetPageMax(ML::CeilingInteger(NbPages));
				if (G_NextPageOnRequestCompletion && G_Prev_PageIndex < NbPages - 1)
					G_Prev_PageIndex += 1;
				G_PageIndex = ML::Min(G_Prev_PageIndex, ML::CeilingInteger(NbPages)-1);
				if (G_PageIndex < 0)
					G_PageIndex = 0;
			} else {
				if (G_SelectLastRoom)
				{
					G_IndexSelectY = RequestedRooms.count-1;
					UpdateSelection();
					G_SelectLastRoom = False;
				}
				SetPageMax(1);
			}
			Update2DArray(True);
			G_CurrentSelectedRoomIndex = G_PageIndex * {{{Count_InstancesY}}} + G_IndexFocusY;
			UpdateInputsInfo();
		}
		
		Void OnRequestedRoomsFailure()
		{
			declare Text[Text][] RequestedRooms for Page;
			
			G_RequestError = True;
			G_SelectLastRoom = False;
			Loading_SetIsLoading(False);
			declare Real NbPages = (RequestedRooms.count*1.0 - 1)/({{{Count_InstancesY}}}*1.0);
			SetPageMax(ML::CeilingInteger(NbPages));
			Update2DArray(True);
			G_CurrentSelectedRoomIndex = G_PageIndex * {{{Count_InstancesY}}} + G_IndexFocusY;
			UpdateInputsInfo();
		}
		
		//////////////////
		// MAIN
		//////////////////
		main() {
			Init();
			
			EnableMenuNavigation(True,False,Null,2);

			declare Text[Text][] RequestedRooms for Page;
			declare Text[Text][] RequestedRoomMaps for Page;			
			declare Text[][] CurrentFilters for Page;
		
			declare Integer ActiveButtonPlatform = {{{Platform}}};
		
			G_HideTime = -1;

			{{{Browser::InjectBeforeLoop()}}}
			{{{ServerInfo::InjectBeforeLoop()}}}
			
			while(True) {
				yield;
				
				if(! PageIsVisible) continue;
				LibManialink_AnimLoop();
				
				{{{Browser::InjectInLoop()}}}
				{{{ServerInfo::InjectInLoop()}}}
				
				if(G_HideTime <= Now && G_HideTime != -1)
				{
					G_HideTime = -1;
					SendCustomEvent("DetachPage", ["ServerBrowser"]);
				}

				{{{Loading::InjectInLoop()}}}
			
				if (Input.PendingEvents.count != 0) {
					declare Integer LastInputPlatform;
					switch (Input.PendingEvents[Input.PendingEvents.count-1].Pad.Type) {
						case CInputPad::EPadType::Keyboard 		: LastInputPlatform = {{{LibUIButtons::GetPlatform("PC")}}};
						case CInputPad::EPadType::PlayStation 	: LastInputPlatform = {{{LibUIButtons::GetPlatform("PS4")}}};
						case CInputPad::EPadType::XBox 			: LastInputPlatform = {{{LibUIButtons::GetPlatform("XB1")}}};
						default 								: LastInputPlatform = {{{LibUIButtons::GetPlatform("PC")}}};
					}
					if (LastInputPlatform != ActiveButtonPlatform)
						SendCustomEvent("SetActiveButtonPlaform", ["ServerBrowser", TL::ToText(LastInputPlatform)]);
				}
			
				//EVENTS
				foreach(Event in PendingEvents) {
					switch(Event.Type) {
						case CMlEvent::Type::EntrySubmit : {
							if (Event.ControlId != "ServerPassword")
								continue;
							ServerInfo_AskPassword_OnValidate();
						}
						case CMlEvent::Type::PluginCustomEvent: {
							switch(Event.PluginCustomEventType) {
								case "Show" : {
									{{{_ShowAnim}}}
									EnableMenuNavigation(True,False,Null,2);
									if (G_IsInRoomManager)
									{
										G_IsInRoomManager = False;
									} else {
										G_PageIndex = 0;
										G_Prev_PageIndex = 0;
										SetOnglet();
									}
									G_ServerBrowser_LastRefreshTime = Now;
									G_ServerInfo_LastRefreshTime = Now;
									ServerBrowserStatus_Switch(C_STATUS_BROWSER);
									SendCustomEvent("ServerBrowser_GetContext", Text[]);
									SendCustomEvent("GetIsLimitedToCanyon", Text[]);
									SendCustomEvent("GetActiveButtonPlatform", ["ServerBrowser"]);
								}
								case "SetActiveButtonPlatform" : {
									ActiveButtonPlatform = TL::ToInteger(Event.CustomEventData[0]);
									Filters_UpdateHelpersForPlatform(ActiveButtonPlatform);
									ServerBrowser_UpdateHelpersForPlatform(ActiveButtonPlatform);
									ServerInfo_UpdateHelpersForPlatform(ActiveButtonPlatform);
								}
								case "Hide" : {
									{{{_HideAnim}}}
									HidePage();
								}
								case "SetOngletFocusNewRoom" : {
									G_OngletFocus = 2;
									SwitchOnglet();
									SendRequestServers();
									G_SelectLastRoom = True;
								}
								case "Request_AllServers_Success" : {
									OnRequestedRoomsReceived(0);
								}
								case "Request_FavoriteServers_Success" : {
									OnRequestedRoomsReceived(1);
								}
								case "Request_MyRooms_Success" : {
									OnRequestedRoomsReceived(2);
								}
								case "Request_MyRooms_Failure" : {
									OnRequestedRoomsFailure();
								}
								case "Request_AllServers_Failure" : {
									OnRequestedRoomsFailure();
								}
								case "Request_FavoriteServers_Failure" : {
									OnRequestedRoomsFailure();
								}
								case "Request_StartRoom_Success": {
									G_Join_Started = True;
									
									if (G_ServerBrowserStatus == C_STATUS_DEBUG)
										Debug_OnStart();
								}
								case "Request_StartRoom_Failure": {
									G_Join_Started = True;
									if (G_JoinStatus != C_JOINSTATUS_ERROR && G_JoinStatus != C_JOINSTATUS_NONE)
									{
										SetJoinStatus(C_JOINSTATUS_ERROR);
									}
								}
								case "Request_ServerStop_Success": {
									if (G_ServerBrowserStatus == C_STATUS_DEBUG)
										Debug_OnStop();
								}								
								case "Request_DeleteRoom_Success": {
									SendRequestServers();
								}
								case "Request_ServerMaps_Success" : {
									if (G_ServerBrowserStatus == C_STATUS_INFORMATION)
										ServerInfo_OnServerMapsReceived();
								}
								case "Request_ServerMaps_Failure" : {
									if (G_ServerBrowserStatus == C_STATUS_INFORMATION)
										ServerBrowserStatus_Switch(C_STATUS_BROWSER); //On est obligés à cause du checktargeted sur les mapAuthors
								}
								case "Request_ServerStatus_Result": {
									G_IsServerOnlineRequestLastTime = Now;
									if (Event.CustomEventData.count < 6)
										continue;
									
									declare ServerIndex = G_PageIndex * {{{Count_InstancesY}}} + G_IndexFocusY; 
									if (ServerIndex >= RequestedRooms.count || RequestedRooms[ServerIndex]["id"] != Event.CustomEventData[0])
										continue; //not the server you're looking for
										
									RequestedRooms[ServerIndex]["required_state"] 	= Event.CustomEventData[1];
									RequestedRooms[ServerIndex]["status"]			= Event.CustomEventData[2];
									RequestedRooms[ServerIndex]["is_online"]		= Event.CustomEventData[3];
									RequestedRooms[ServerIndex]["debug_logs"]		= Event.CustomEventData[4];
									RequestedRooms[ServerIndex]["starting"]			= Event.CustomEventData[5];
									
									if (G_ServerBrowserStatus == C_STATUS_DEBUG)
										Debug_Update(Event.CustomEventData[1], Event.CustomEventData[2], Event.CustomEventData[4]);
									if (G_ServerBrowserStatus == C_STATUS_INFORMATION)
									{
										if (G_JoinStatus == C_JOINSTATUS_STATUS)
											StartJoin();
										else
											ServerInfo_UpdateFromStatus();
									}
								}
								case "Request_ServerStatus_Error" : {
									if (G_ServerBrowserStatus == C_STATUS_INFORMATION)
										ServerBrowserStatus_Switch(C_STATUS_BROWSER);
								}
								case "Check_PlayMultiplayerSession_Succeeded" : {
									if (G_ServerBrowserStatus == C_STATUS_INFORMATION)
										ServerInfo_OnJoinServerPrivilegeChecked();
								}
								case "Check_PlayMultiplayerSession_Failed" : {
									G_ServerInfo_IsCheckingNpAvailabilityForJoin = False;
									Quit();
								}
								case "SetIsLimitedToCanyon" : {
									declare Boolean IsLimitedToCanyon = Event.CustomEventData[0] == "True";
									if (IsLimitedToCanyon != G_IsLimitedToCanyon)
									{
										G_IsLimitedToCanyon = IsLimitedToCanyon;
										
										declare CMlFrame  FrameInstance_Landmark <=> (Page.GetFirstChild("FrameInstance_Landmark2") as CMlFrame);
										declare CMlQuad   Quad_LandmarkName <=> (FrameInstance_Landmark.GetFirstChild("Quad_LandmarkName") as CMlQuad);
										if (G_IsLimitedToCanyon)
											Quad_LandmarkName.BgColor = <.6, .6, .6>;
										else
											Quad_LandmarkName.BgColor = <0., 226./256., 1.>;
										
										if (G_IsLimitedToCanyon)
										{
											if (G_OngletFocus == 2)
											{
												G_OngletFocus = 1;
												SetOnglet();
												SendCustomEvent("PopUp_MyRoomsRestricted", Text[]);
											} else {
												SendCustomEvent("PopUp_CanyonRestricted", Text[]);
											}
										}
										if (G_IsLimitedToCanyon || G_ServerBrowserStatus == C_STATUS_BROWSER)
											ServerBrowserStatus_Switch(C_STATUS_BROWSER);
									}
								}
							}
						}
						case CMlEvent::Type::KeyPress: {
							if (G_ServerBrowserStatus == C_STATUS_INFORMATION) {
								{{{ServerInfo::InjectInKeyPress()}}}
							} else if (G_ServerBrowserStatus == C_STATUS_BROWSER) {
								{{{Browser::InjectInKeyPress()}}}
							}
						}
						case CMlEvent::Type::MouseClick: {
							if (ActiveButtonPlatform !=  {{{LibUIButtons::GetPlatform("PC")}}})
								SendCustomEvent("SetActiveButtonPlaform", ["ServerBrowser", TL::ToText({{{LibUIButtons::GetPlatform("PC")}}})]);
							switch (G_ServerBrowserStatus)
							{
								case C_STATUS_JOIN : continue;
								case C_STATUS_DEBUG : continue;
								case C_STATUS_INFORMATION :
								{
									{{{ServerInfo::InjectInMouseClick()}}}	
								}
								case C_STATUS_FILTERS :
								{
									{{{Filters::InjectInMouseClick()}}}
								}
								case C_STATUS_BROWSER :
								{
									{{{Browser::InjectInMouseClick()}}}
								}
							}
						}
						case CMlEvent::Type::MouseOver: {
							switch (G_ServerBrowserStatus)
							{
								case C_STATUS_JOIN : continue;
								case C_STATUS_DEBUG : continue;
								case C_STATUS_INFORMATION : {
									{{{ServerInfo::InjectInMouseOver()}}}
								}
								case C_STATUS_FILTERS :
								{
									{{{Filters::InjectInMouseOver()}}}
								}
								case C_STATUS_BROWSER :
								{
									{{{Browser::InjectInMouseOver()}}}
								}
							}
						}
						case CMlEvent::Type::MouseOut: {
							switch (G_ServerBrowserStatus) {
								case C_STATUS_JOIN : continue;
								case C_STATUS_DEBUG : continue;
								case C_STATUS_INFORMATION : {
									{{{ServerInfo::InjectInMouseOut()}}}
								}
								case C_STATUS_FILTERS : {
									{{{Filters::InjectInMouseOut()}}}
								}
								case C_STATUS_BROWSER : {
									{{{Browser::InjectInMouseOut()}}}
								}
							}
						}
						case CMlEvent::Type::MenuNavigation: {
							if(G_LockInput + 50 > Now) continue;
							switch (G_ServerBrowserStatus)
							{
								case C_STATUS_JOIN : continue;
								case C_STATUS_DEBUG :
								{
									{{{Debug::InjectInMenuNavigation()}}}
								}
								case C_STATUS_INFORMATION :
								{
									{{{ServerInfo::InjectInMenuNavigation()}}}	
								}
								case C_STATUS_FILTERS :
								{
									{{{Filters::InjectInMenuNavigation()}}}
								}
								case C_STATUS_BROWSER :
								{
									G_LockInput = Now; // Sécurité pour éviter le double input
									{{{Browser::InjectInMenuNavigation()}}}
								}
							}
						}
					}
				}
			}
		}
	--></script>
	""";
	
	return MLText;
}
